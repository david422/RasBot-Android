buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.7'
    }
}
/*
allprojects {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
}*/

apply plugin: 'com.android.application'

apply plugin: 'io.fabric'


ext{
    def localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
    gstDir = localProperties.getProperty("gst.dir")
    ndkDir = localProperties.getProperty("ndk.dir")

}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    defaultConfig {
        applicationId "pl.dp.rasbot"
        minSdkVersion 21
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"

    }

    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
        java.srcDirs += 'src/main/jni/src'
    }

    tasks.withType(Exec) {

        environment = [:]
        environment['PATH'] = System.getenv("PATH")+ ":/usr/local/bin"
        environment['GSTREAMER_SDK_ROOT'] = gstDir
    }

    task showVar(){
        println("project ndk: " + ndkDir)
        println("project gst: " + gstDir)
    }

    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {

        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                '-j', Runtime.runtime.availableProcessors(),
                'all',
                'NDK_DEBUG=1',
                'V=1',
                'APP_PLATFORM=android-19'

    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                'clean'
    }

    clean.dependsOn 'cleanNative'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}
//configurations {
//    compile.exclude group:'ch.qos.logback'
//}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.squareup:otto:1.3.6'

    compile 'com.github.navasmdc:MaterialDesign:1.5@aar'

//    compile 'com.afollestad.material-dialogs:core:0.8.6.2'

    compile 'com.afollestad:material-dialogs:0.7.8.1'


    compile 'com.googlecode.gstreamer-java:gstreamer-java:1.5'
}
