buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.7'
    }
}

apply plugin: 'com.android.application'

apply plugin: 'io.fabric'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'

ext{
    def localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
    gstDir = localProperties.getProperty("gst.dir")
    ndkDir = localProperties.getProperty("ndk.dir")

}

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.1'

    defaultConfig {
        applicationId "pl.dp.rasbot"
        minSdkVersion 18
        targetSdkVersion 19
        versionCode 2
        versionName "1.1"
    }

    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
        java.srcDirs += 'src/main/jni/src'
    }

    tasks.withType(Exec) {

        environment = [:]
        environment['PATH'] = System.getenv("PATH")+ ":/usr/local/bin"
        environment['GSTREAMER_SDK_ROOT'] = gstDir
    }

    task showVar(){
        println("project ndk: " + ndkDir)
        println("project gst: " + gstDir)
    }

    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {

        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                '-j', Runtime.runtime.availableProcessors(),
                'all',
                'NDK_DEBUG=1',
                'V=1',
                'APP_PLATFORM=android-19'

    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                'clean'
    }

    clean.dependsOn 'cleanNative'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

retrolambda {
    jdk "/usr/lib/jvm/java-8-oracle"
    oldJdk "/usr/lib/jvm/java-7-oracle"
    javaVersion JavaVersion.VERSION_1_7
    defaultMethods false
    incremental true
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:design:24.1.1'

    compile 'com.jakewharton:butterknife:8.1.0'
    apt 'com.jakewharton:butterknife-compiler:8.1.0'
    compile 'com.squareup:otto:1.3.8'

    compile 'com.afollestad.material-dialogs:core:0.9.0.0'

    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'

    compile 'com.jakewharton.timber:timber:4.1.2'
}
